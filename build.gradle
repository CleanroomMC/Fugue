buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        maven { url = 'https://maven.outlands.top/releases' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.10.+', changing: true
        classpath "org.spongepowered:mixingradle:0.7.+"
        classpath "cl.franciscosolis:blossom:1.3.1" 
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'cl.franciscosolis.blossom-extended'

version = "${minecraft_version}-${mod_version}"
group = mod_base_package
archivesBaseName = mod_id



java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {

    mappings channel: mappings_channel, version: mappings_version

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    legacy {
        fixClasspath = true
        extractMappings = true
        attachMappings = true
    }
    
    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.debug', 'true'
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
mixin {add sourceSets.main, mod_id + "mixins.refmap.json"}
repositories {
    maven {
        name 'Cleanroom Maven'
        url 'https://maven.cleanroommc.com/'
    }
    maven {
        url "https://repo.cleanroommc.com/snapshots"
    }
    maven {
        url "https://maven.outlands.top/releases/"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    mavenCentral()
}

dependencies {
    minecraft "com.cleanroommc:cleanroom:15.24.0.3024"
    //Charset Lib
    compileOnly(fg.deobf("curse.maven:charset-284523:3656492"))
    runtimeOnly(fg.deobf("curse.maven:charset-284523:3656492"))
    //Xaero's Minimap
    compileOnly(fg.deobf("curse.maven:xaero-263420:4911777"))
    runtimeOnly(fg.deobf("curse.maven:xaero-263420:4911777"))
    compileOnly(fg.deobf("curse.maven:xaero-plus-866084:4912433"))
    //runtimeOnly(fg.deobf("curse.maven:xaero-plus-866084:4912433")) //Don't uncomment it or you will crash
}

processResources {
    blossom {
        String mcmod = "src/main/resources/mcmod.info"
        String mcmeta = "src/main/resources/pack.mcmeta"
        replaceToken("mod_id", mod_id, mcmod)
        replaceToken("mod_name", mod_name, mcmod)
        replaceToken("mod_description", mod_description, mcmod)
        replaceToken("mod_version", mod_version, mcmod)
        replaceToken("mod_authors", mod_authors, mcmod)
        replaceToken("minecraft_version", minecraft_version, mcmod)
        replaceToken("mod_id", mod_id, mcmeta)
    }
}

blossom {
    String reference = "src/main/java/com/cleanroommc/Reference.java"
    replaceToken("@VERSION@", mod_version, reference)
    replaceToken("@MODNAME@", mod_name, reference)
    replaceToken("@MODID@", mod_id, reference)
}

jar {
    manifest {
        attributes([
                'Specification-Title': mod_id,
                'Specification-Vendor': mod_authors,
                'Specification-Version': '1', // We are version 1 of ourselves
                //'FMLCorePluginContainsFMLMod': true,
                //'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                //'FMLCorePlugin': '',
                //'ForceLoadAsMod': true,
                //'FMLAT': '',
                'Implementation-Title': project.name,
                'Implementation-Version': mod_version,
                'Implementation-Vendor': mod_authors,
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
